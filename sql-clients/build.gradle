description = 'SQL clients for Elasticsearch and code shared between those ' +
    'clients and their backends in :x-pack-elasticsearch:plugin'

import org.gradle.plugins.ide.eclipse.model.*;
import org.elasticsearch.gradle.precommit.PrecommitTasks

subprojects {
  apply plugin: 'elasticsearch.build'

  sourceSets.test.resources.srcDirs = ["src/test/resources", "src/test/java"]

  // Massaging Eclipse
  eclipse {
    classpath.file {
      whenMerged { cp ->
        def con = entries.find { e ->
          e.kind == "con" && e.toString().contains("org.eclipse.jdt.launching.JRE_CONTAINER")
        }
        ((AbstractClasspathEntry) con).accessRules.add(new AccessRule("accessible", "com/sun/net/httpserver/*"))

        entries.unique { a, b ->
          return a.path.compareTo(b.path)
        }
        entries.removeAll { it.path.endsWith('.pom') }
      }
    }
  }

  dependencies {
    testCompile("junit:junit:${versions.junit}") {
      exclude group:'org.hamcrest', module:'hamcrest-core'
    }
    testCompile "org.hamcrest:hamcrest-all:${versions.hamcrest}"
  }

  tasks.checkstyle.enabled = false // not worth it yet
  tasks.checkstyleTest.enabled = false // not worth it yet

  forbiddenApisMain {
    // does not depend on core, so only jdk and http signatures should be checked
    signaturesURLs = [PrecommitTasks.getResource('/forbidden/jdk-signatures.txt')]
  }

  forbiddenApisTest {
    // does not depend on core, so only jdk signatures should be checked
    signaturesURLs = [PrecommitTasks.getResource('/forbidden/jdk-signatures.txt')]
  }
}
