apply plugin: 'elasticsearch.standalone-rest-test'
apply plugin: 'elasticsearch.rest-test'

dependencies {
  testCompile project(path: ':x-pack-elasticsearch:plugin', configuration: 'runtime')
  testCompile project(path: ':x-pack-elasticsearch:transport-client', configuration: 'runtime')
}

String outputDir = "generated-resources/${project.name}"
task copyXPackPluginProps(type: Copy) {
  from project(':x-pack-elasticsearch:plugin').file('src/main/plugin-metadata')
  from project(':x-pack-elasticsearch:plugin').tasks.pluginProperties
  into outputDir
}
project.sourceSets.test.output.dir(outputDir, builtBy: copyXPackPluginProps)

integTestRunner {
  systemProperty 'tests.security.manager', 'false'
}

integTestCluster {
  plugin ':x-pack-elasticsearch:plugin'
  setupCommand 'setupDummyUser',
               'bin/x-pack/users', 'useradd', 'test_user', '-p', 'changeme', '-r', 'superuser'
  setupCommand 'setupTransportClientUser',
               'bin/x-pack/users', 'useradd', 'transport', '-p', 'changeme', '-r', 'transport_client'
  waitCondition = { node, ant ->
    File tmpFile = new File(node.cwd, 'wait.success')
    ant.get(src: "http://${node.httpUri()}",
            dest: tmpFile.toString(),
            username: 'test_user',
            password: 'changeme',
            ignoreerrors: true,
            retries: 10)
    return tmpFile.exists()
  }
}
