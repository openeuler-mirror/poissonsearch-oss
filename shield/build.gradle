apply plugin: 'elasticsearch.esplugin'
esplugin {
  name 'shield'
  description 'Elasticsearch Shield (security)'
  classname 'org.elasticsearch.shield.ShieldPlugin'
  isolated false
}

configurations {
  licensePluginZip
}

dependencies {
  licensePluginZip project(path: "${projectsPrefix}:license:plugin") // zip
  provided project(path: "${projectsPrefix}:license:plugin", configuration: 'runtime')
  compile project("${projectsPrefix}:license:plugin-api")
  compile 'dk.brics.automaton:automaton:1.11-8'
  compile 'com.unboundid:unboundid-ldapsdk:2.3.8'
  testCompile 'org.slf4j:slf4j-log4j12:1.6.2'
  testCompile 'org.elasticsearch:securemock:1.1'
  testCompile 'com.google.jimfs:jimfs:1.0'
}

forbiddenPatterns {
  exclude '**/*.p12'
}

compileJava.options.compilerArgs << "-Xlint:-deprecation,-rawtypes,-serial,-try,-unchecked"
compileTestJava.options.compilerArgs << "-Xlint:-deprecation,-rawtypes,-serial,-try,-unchecked"

// no integ tests...
integTest.enabled = false

// TODO: standardize packaging config for plugins
bundlePlugin {
  from(projectDir) {
    include 'LICENSE.txt'
    include 'NOTICE.txt'
  }
  from('bin/shield') {
    into 'bin'
  }
  from('config/shield') {
    into 'config'
  }
}
