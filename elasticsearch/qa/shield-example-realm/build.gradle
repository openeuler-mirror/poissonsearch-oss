import org.elasticsearch.gradle.MavenFilteringHack
import org.elasticsearch.gradle.VersionProperties

apply plugin: 'elasticsearch.build'

dependencies {
  provided "org.elasticsearch:elasticsearch:${versions.elasticsearch}"
  testCompile "org.elasticsearch.test:framework:${project.versions.elasticsearch}"
  provided project(path: ':x-plugins:elasticsearch:x-pack', configuration: 'runtime')
}

Map generateSubstitutions() {
  def stringSnap = { version ->
    if (version.endsWith("-SNAPSHOT")) {
      return version.substring(0, version.length() - 9)
    }
    return version
  }
  return [
    'version': stringSnap(version),
    'xpack.version': stringSnap(VersionProperties.elasticsearch),
    'java.version': targetCompatibility as String
  ]
}

processResources {
  MavenFilteringHack.filter(it, generateSubstitutions())
}

task buildZip(type:Zip, dependsOn: [jar]) {
  from 'build/resources/main/xpack-extension-descriptor.properties'
  from project.jar
}

task integTest(type: org.elasticsearch.gradle.test.RestIntegTestTask, dependsOn: buildZip) {
  cluster {
    plugin 'x-pack', project(':x-plugins:elasticsearch:x-pack')
    // TODO: these should be settings?
    setting 'shield.authc.realms.custom.order', '0'
    setting 'shield.authc.realms.custom.type', 'custom'
    setting 'shield.authc.realms.esusers.order', '1'
    setting 'shield.authc.realms.esusers.type', 'esusers'

    setupCommand 'setupDummyUser',
                 'bin/xpack/esusers', 'useradd', 'test_user', '-p', 'changeme', '-r', 'admin'
    setupCommand 'installExtension',
                  'bin/xpack/extension', 'install', 'file:' + buildZip.archivePath
    waitCondition = { node, ant ->
      File tmpFile = new File(node.cwd, 'wait.success')
      ant.get(src: "http://${node.httpUri()}",
              dest: tmpFile.toString(),
              username: 'test_user',
              password: 'changeme',
              ignoreerrors: true,
              retries: 10)
      return tmpFile.exists()
    }
  }
}
check.dependsOn integTest
integTest.mustRunAfter precommit