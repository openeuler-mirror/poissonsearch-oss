import org.elasticsearch.gradle.precommit.PrecommitTasks

apply plugin: 'elasticsearch.esplugin'

esplugin {
    name 'ml'
    description 'Machine Learning Plugin'
    classname 'org.elasticsearch.xpack.ml.MlPlugin'
}

if (findProject(':machine-learning-cpp') != null) {
  configurations.archives {
      resolutionStrategy.dependencySubstitution {
          substitute module("org.elasticsearch.ml:ml-cpp") with project(":machine-learning-cpp")
      }
  }
  bundlePlugin.dependsOn ':machine-learning-cpp:buildUberZip'
}


configurations {
    archives
}

dependencies {
    compile group: 'net.sf.supercsv', name: 'super-csv', version:"${supercsvVersion}"
    archives group: "${project.group}", name: 'ml-cpp', version:"${project.version}", ext: 'zip'
    testCompile group: 'org.ini4j', name: 'ini4j', version:"${ini4jVersion}"
}

integTest {
    cluster {
        //setting 'useNativeProcess', 'true'
        distribution = 'zip'
    }
}

bundlePlugin {
  for (outputFile in configurations.archives) {
    from(zipTree(outputFile)) {
      duplicatesStrategy 'exclude'
    }
  }
}
